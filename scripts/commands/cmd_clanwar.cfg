// clanwar script - by star
// modified by sauerduels

countdown_time = 5  // time in seconds
ready_progress = -1
last_ready_cn = -1
reset_spawns = 0

countdown  = [
    ready_progress = -1
	say ( format "^f1Game will start in ^f6%1 ^f1seconds, prepare yourself! Good luck and have fun!" $countdown_time)
	mutemode 1
	
	asleep (* $countdown_time 1000)	[
		pause 0
		ready_progress = 0
		say ( format "^f0GO!" )
		all = (allplayers)
		if ($reset_spawns) [
		    reset_spawns = 0
		    looplist cn $all [
			    spawnplayer $cn 1
		    ]
		]
	]
	
	loop i ( - $countdown_time 1) [
		asleep (* (+ $i 1) 1000 ) [ say ( format "^f1Starting in ^f6%1 ^f1seconds..." ( - @countdown_time (+ @i 1) ) )]
	]
]

registercommand "startcw" countdown 3 "" "startcw - Resume clanwar after countdown and push new player spawns"

startclanwar = [ 
	pigvar = 0 	// disable pigmode if enabled!
	if (= (indexof $MODENAMES $arg2) -1) [
		say (format "^f3wrong argument: ^f7%1" $arg2)
	][
		mapmode $arg3 (indexof $MODENAMES $arg2)
		pause 1
		autopause_on = 1
		ready_progress = 0
		reset_spawns = 1
		// uncomment the following line to start the countdown automatically
		//countdown
	]
]

registercommand "cw" startclanwar 3 "ww" "cw <mode> <map> - Start clanwar with countdown."

clanwar_ready = [
    if (ispaused) [
        if (&& (= $ready_progress 1) (! (isspectator $arg1)) (!= $arg1 $last_ready_cn)) [
            ready_progress = -1
            last_ready_cn = $arg1
            say (concatword "^f6" (getname $arg1) "^f1 is ready!")
            countdown
        ]
        if (&& (= $ready_progress 0) (! (isspectator $arg1))) [
            ready_progress = 1
            last_ready_cn = $arg1
            say (concatword "^f6" (getname $arg1) "^f1 is ready!")
        ]
    ]
]

registercommand "ready" clanwar_ready 1 "" "ready - Player is ready to start the countdown."

