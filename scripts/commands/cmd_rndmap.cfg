// select 3 random maps from mappool

effic_pool = [aard3c academy frozen memento ot simplicity turbine]
ffa_pool = [corruption hades metl3 metl4 nmp8 paradigm tumwalk]
insta_pool = [complex duel7 douze frozen kffa ot turbine]
pool_size = 5

// pickmap <reference to list>, returns picked map and removes it from list
pick_map = [
  local pick
  pick = (rnd (listlen $$arg1))
  result (at $$arg1 $pick)
  $arg1 = (listsplice $$arg1 [] $pick 1)
]

// generate_selection <list> <size>
generate_selection = [
  local temp_pool
  temp_pool = $arg1
  sortlist (loopconcat _ $arg2 [pick_map temp_pool]) a b [<s $a $b]
]

cmd_rndmap = [
  say (format "^fs^f3The random map choice are:^fr %1" (generate_selection $[@[arg2]_pool] $pool_size))
]

registercommand "rndmap" cmd_rndmap 3 "s" "rndmap [mode] ^f1Randomly select 5 maps from the map pool"

